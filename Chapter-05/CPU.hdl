// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    DMux(in=true, sel=instruction[15], a=ainst, b=cinst);

    Alias(in=instruction[12], out=useMemAsY);
    Alias(in=instruction[5], out=destA);
    Alias(in=instruction[4], out=destD);
    Alias(in=instruction[3], out=destM);

    And(b=cinst, a=destD, out=setD);
    And(b=cinst, a=destM, out=writeM);

    //computing jump condition: j1=instr[2], j2=instr[1], j3=instr[0]
    Not(in=aluZero, out=alulNotZero);
    Not(in=aluNeg, out=alulNotNeg);

    And(a=instruction[2], b=aluNeg, out=jmpLt);
    And(a=instruction[1], b=aluZero, out=jmpEq);
    And3(a=instruction[0], b=alulNotZero, c=alulNotNeg, out=jmpGt);

    Or3(a=jmpLt, b=jmpEq, c=jmpGt, out=jmpAll);

    And(a=jmpAll, b=cinst, out=shouldJump);

    //ARegister setup
    Or(a=destA, b=ainst, out=setA);
    Mux16(a=aluOut, b=instruction, sel=ainst, out=aIn);
    ARegister(
        in=aIn,
        load=setA,
        out[0..14]=addressM,
        out=aOut
        );

    //DRegister setup
    DRegister(
        in=aluOut,
        load=setD,
        out=aluX
        );

    //Counter setup
    PC(
        in=aOut,
        load=shouldJump,
        inc=true,
        reset=reset,
        out[0..14]=pc);

    //ALU setup
    Mux16(a=aOut, b=inM, sel=useMemAsY, out=aluY);

    ALU(
        x=aluX,
        y=aluY,
        zx=instruction[11],
        nx=instruction[10],
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],

        out=aluOut,
        out=outM,
        zr=aluZero,
        ng=aluNeg
    );
}